# Flax to Axon - Full

```elixir
Mix.install([
  {:axon, "~> 0.6.1"},
  {:stream_data, "~> 1.1"},
  {:nx, "~> 0.7.2"},
  {:safetensors, "~> 0.1.3"},
  {:kino, "~> 0.12.3"},
  {:langchain, "~> 0.2.0"}  
])

asdf_dir = "#{__DIR__}/.asdf"

unless File.exists?(asdf_dir) do
  {_,0} = System.cmd("git", [
    "clone",
    "https://github.com/asdf-vm/asdf.git",
    asdf_dir,
    "--branch",
    "v0.14.0"
  ])
end

asdf = "#{asdf_dir}/bin/asdf"
{_, 0} = System.cmd(asdf, ["plugin", "add", "python"], env: [{"ASDF_DATA_DIR", asdf_dir}])

{_, 0} = System.cmd(asdf, ["install", "python", "3.11.9"], env: [{"ASDF_DATA_DIR", "#{__DIR__}/.asdf"}])

asdf_python = Path.join([asdf_dir, "installs", "python", "3.11.9", "bin", "python"])

python_packages =
  ~w(
    safetensors 
    torch
    transformers
    accelerate 
    numpy
    datasets
    pillow
    flax
    jax
    jaxlib
  )

venv_dir = Path.join(__DIR__, "flax2axon_env")
{_, 0} = System.cmd(asdf_python, ["-m", "venv", "--copies", venv_dir])

python = Path.join([venv_dir, "bin", "python"])
pip = Path.join([venv_dir, "bin", "pip"])

{_, 0} = System.cmd(pip, ["install" | python_packages])

run_python = fn command, opts ->
  System.cmd(python, ["-c", command], opts)
end

data_dir = Path.join(__DIR__, "data")

unless File.exists?(data_dir), do: :ok = File.mkdir(data_dir)
```

## Plan

```elixir

```

```elixir
transformers_path = Path.join(venv_dir, "lib/python3.11/site-packages/transformers")
resnet_flax_path = Path.join(transformers_path, "models/resnet/modeling_flax_resnet.py")

{:ok, resnet_input} = File.read(resnet_flax_path)
```

## Get inputs

```elixir
input = resnet_input
```

We get the individual classes

```elixir
input_classes = fn input ->
[_ | classes] = String.split(input, "\nclass ")
  for class <- classes, do: "class #{class}"
end
```

```elixir
extract_names = fn flax_code ->
  [flax_model_name | _] = String.split(flax_code, "(")
  flax_model_name = String.trim_leading(flax_model_name, "class ")
  axon_model_function_name = Macro.underscore(flax_model_name) 
  {flax_model_name, axon_model_function_name}
end
```

Might need to figure out which classes depend on which other classes. Then, we do the following for each of them.

## Conversion

```elixir
alias LangChain.Chains.LLMChain
alias LangChain.ChatModels.ChatOpenAI
alias LangChain.Message
```

Define system messages to make this work better.

```elixir
system_messages =
  [
    """
    You are an expert in machine learning frameworks.
    You help converting models from Python code in Flax linen framework to Elixir code in the Axon framework.
    """,
    """
    You will do this by following instructions step by step.
    """,
    """
    I will provide you with the model code and an instruction in the following format:
    INSTRUCTION: here is the instruction you perform
    MODEL: here is the model code you will modify
    """,
    """
    For each of the following instructions, reply only with the modified code of the model.
    Do NOT include any other content or comments.
    """,
    """
    Perform ONLY the action the instruction asks you to do.
    """,
    """
    In case you don't change anything return ONLY the unchanged model code.
    """
  ]
  |> Enum.map(&Message.new_system!/1)
```

```elixir
conversion_layers =
  """
  This is a conversion table you must use to convert Flax layers to Axon layers.
  | Framework | Layer | Params |     |     |     |     |     |     |     |     |     |     |     |     |     |     |     |
  | --------- | ----- | ------ | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- | --- |
  | Flax | nn.Conv   | features                  | kernel_size | strides | padding | dtype | use_bias | kernel_init        |
  | Axon | Axon.conv | units (second pos. param) | kernel_size | strides | padding | -     | use_bias | kernel_initializer |
  | Hints | | | single number instead of tuple | | predefined values (:valid) |
  | --- | 
  |Flax| nn.BatchNorm | use_running_average | axis | momentum | epsilon |dtype | param_dtype | use_bias |use_scale | bias_init |scale_init |axis_name |axis_index_groups |use_fast_variance |force_float32_reductions |parent |name |
  |Axon| Axon.batch_norm | - | - | - | epsilon | - | - | - | - | beta_initializer(?) | gamma_initializer(?) | - | - |- | - | - |name |
  | Hints | | | | | |
  """

Kino.Markdown.new(conversion_layers) |> Kino.render()

conversion_initializers =
  """
   This is a conversion table you must use to convert Flax initializers to Axon initializers.
  | Framework | Initializer                      | Params         |               |                       |         |          |            |       |
  | --------- | -------------------------------- | -------------- | ------------- | --------------------- | ------- | -------- | ---------- | ----- |
  | Flax      | nn.initializers.variance_scaling | scale (1. pos) | mode (2. pos) | distribution (3. pos) | in_axis | out_axis | batch_axis | dtype |
  | Axon  | Axon.Initializers.variance_scaling | scale | mode | distribution |
  | Hints |                                    |       |      |              |
  """

Kino.Markdown.new(conversion_initializers) |> Kino.render()

conversion_activations =
  """
  Convert activations
  """

conversion_messages =
  [
    conversion_layers,
    conversion_initializers,
    conversion_activations
  ]
  |> Enum.map(&Message.new_system!/1)
```

Define the langchain

```elixir
api_key = System.get_env("LB_OPENAI_KEY") 
```

```elixir
defmodule ConversionChain do
  def conversion_chain(api_key, system_messages, conversion_messages) do
    {:ok, chain, _response} =
      %{
        llm:
          ChatOpenAI.new!(%{
            model: "gpt-4o",
            api_key: api_key,
            seed: 0
          })
      }
      |> LLMChain.new!()
      |> LLMChain.add_messages(system_messages)
      |> LLMChain.add_messages(conversion_messages)
      |> LLMChain.run()

    chain
  end
end
```

```elixir
send_user_message = fn chain, message ->
  chain
  |> LLMChain.add_message(Message.new_user!(message))
  |> LLMChain.run()
end
```

### Detailed instructions how to convert the model

```elixir
conversion_instructions = [
  """
  If there is a setup function, replace the calls to the stored methods in __call__ with the actual Flax layer, including the parameters.
  """,
  """
  If there are attributes, move them to the __call__ function as parameters and replace all the references of the attributes with the function parameters.
  """,
  """
  In the __call__ function, move all additional parameters when calling the layers to the initialization of the layers. Each layer should be called with a single argument.
  """,
  """
  Extract loops to a seperate function, transform this function according to our rules. Plug the function into the pipeline.
  """,
  """
  Replace the Flax layers with the corresponding Axon layers according to our conversion table. Return only the Elixir code for the model. Take into account the parameters in the first parenthesis. Replace initializer functions with the corresponding Axon functions. Follow these rules to replace activation functions: If there is an activation parameter, and the code makes use of ACT2FN[activation], replace that with Axon.activation(activation). If there is an actual activation function called, replace it with Axon's activation function according to the conversion table. Replace all = for arguments with :
  """,
  """
  Remove the return keyword at the end of the __call__ function. Keep what would be returned on this line.
  """,
  """
   Wrap the Axon model in a function that takes all the required parameters from the __call__ function
  - name the function corresponding to the class name, but snake case.
  - take the same arguments as the __call__ function
  - remove the self argument
  - remove the dtype argument
  - remove the type specs if present
  - wrap the function in do ... end
  """,
  """
  Remove all `use ...` directives from the Elixir code.
  """,
  """
  Check if the function is valid Elixir code. Otherwise, fix all issues by converting Python expressions to Elixir expressions. E.g. // corresponds to div, scientific notation like 1e-05 needs a decimal point in Elixir 1.0e-05.
  """
]
```

```elixir
instruction_message = fn instruction, model ->
  """
  INSTRUCTION: #{instruction}
  MODEL:
  #{model}
  """
end
```

### Extra chain for fixing general Elixir issues at the end

````elixir
defmodule FixChain do
  def fix_chain(api_key) do
    {:ok, chain, _response} =
      %{
        llm:
          ChatOpenAI.new!(%{
            model: "gpt-4o",
            api_key: api_key,
            seed: 0
          })
      }
      |> LLMChain.new!()
      |> LLMChain.add_message(
        Message.new_system!("You are an expert in Python, Flax, Elixir and Axon.")
      )
      |> LLMChain.run()

    chain
  end

  defp fix_instruction(code) do
    """
    Fix this Elixir code according to the error message.
    Reply ONLY with the modified Elixir code.
    If you don't modify anything reply with the unchanged code.
    #{code}
    """
  end

  def fix_model(chain, model) do
    fix_instruction = fix_instruction(model)

    {:ok, _chain, response} =
      chain
      |> LLMChain.add_message(Message.new_user!(fix_instruction))
      |> LLMChain.run()

    response.content
  end

  def maybe_fix_model(model, chain, retries) do
    if retries == 0 do
      {:error, model}
    else
      dbg("trying to fix #{retries}")

      model =
        model
        |> String.trim_leading("```elixir")
        |> String.trim_trailing("```")

      dbg(model)
      
      try do
        ## check that it's valid code 
        Code.eval_string(model, [], __ENV__)
      rescue
        _ -> fix_model(chain, model) |> maybe_fix_model(chain, retries - 1)
      else
        _ -> {:ok, model}
      end
    end
  end
end
````

Build the model

```elixir
convert_model = fn conversion_chain, fix_chain, model ->
  {_, converted_model} =
    for instruction <- conversion_instructions, reduce: {conversion_chain, model} do
      {c, m} ->
        {:ok, updated_chain, response} =
          send_user_message.(c, instruction_message.(instruction, m))

        {updated_chain, response.content}
    end

  case FixChain.maybe_fix_model(converted_model, fix_chain, 3) do
    {:ok, model} -> {:ok, model}
    {:error, model} -> {:conversion_failed, model}
  end
end
```

```elixir
apply_model_fn = fn module_name, model_fn_name, args ->  
  apply(module_name, String.to_atom(model_fn_name), args)  
end
```

## Verify

### Check that Python installation works

```elixir
IO.puts("Python is here: #{python}")
{_, 0} = run_python.("print('hello from Python')", [])
```

Define paths for `safetensors` files, we will use those to work on the same numbers in Python and Elixir.

```elixir
safetensor_files = fn dir, name ->
  {Path.join(dir, "#{name}_params_axon.safetensors"),
   Path.join(dir, "#{name}_params_flax.safetensors"),
   Path.join(dir, "#{name}_test_data_axon.safetensors"),
   Path.join(dir, "#{name}_test_data_flax.safetensors")}
end
```

```elixir
defmodule ParamsUtils do
  def flatten_keys(%{} = params) do
    for key <- Map.keys(params) do
      prefixed_keys(params[key], key)
    end
    |> List.flatten()
  end

  defp prefixed_keys(%Nx.Tensor{}, key), do: key

  defp prefixed_keys(%{} = params, prefix) do
    for key <- Map.keys(params) do
      prefixed_keys(params[key], "#{prefix}.#{key}")
    end
  end

  def get_from_flattened_key(params, flattened_key) do
    keys = String.split(flattened_key, ".")

    for key <- keys, reduce: params do
      acc -> acc[key]
    end
  end

  def unflatten_and_put(params, flattened_key, value) do
    single_param_map = flattened_map(flattened_key, value)

    merge_recursive(params, single_param_map)
  end

  def merge_recursive(%{} = map1, %{} = map2) do
    Map.merge(map1, map2, fn _k, m1, m2 -> merge_recursive(m1, m2) end)
  end

  defp flattened_map(flattened_key, value) do
    case String.split(flattened_key, ".", parts: 2) do
      [key] -> %{key => value}
      [key, other_keys] -> %{key => flattened_map(other_keys, value)}
    end
  end
end
```

Must find some way to map params (using LLM?)

```elixir
get_param_mapping = fn ->
  param_mapping = %{
    "batch_norm_0.beta" => "params.normalization.bias",
    "batch_norm_0.gamma" => "params.normalization.scale",
    "batch_norm_0.mean" => "batch_stats.normalization.mean",
    "batch_norm_0.var" => "batch_stats.normalization.var",
    "conv_0.kernel" => "params.convolution.kernel"
  }
end
```

Write `safetensors` files for Axon and Flax params.

```elixir
save_params = fn params, param_mapping, axon_params_path, flax_params_path ->
  axon_params =
    for {axon_key, _} <- param_mapping, into: %{} do
      {axon_key, ParamsUtils.get_from_flattened_key(params, axon_key)}
    end

  flax_params =
    for {axon_key, flax_key} <- param_mapping, into: %{} do
      {flax_key, ParamsUtils.get_from_flattened_key(params, axon_key)}
    end

  Safetensors.write!(axon_params_path, axon_params)
  Safetensors.write!(flax_params_path, flax_params)
end
```

```elixir
model_string_fn = fn model_code ->
  """
  defmodule ModelTest do
  #{model_code}
  end
  """
end
```

### Run tests with random data

<!-- livebook:{"break_markdown":true} -->

First in Axon.

```elixir
run_axon_and_save = fn predict_fn, params, input_shape, path ->
  input_data =
    for dim <- Enum.reverse(Tuple.to_list(input_shape)), reduce: StreamData.float() do
      acc -> StreamData.list_of(acc, length: dim)
    end

  test_data =
    for i <- 0..100 do
      input =
        input_data
        |> Enum.take(1)
        |> hd
        |> Nx.tensor()

      input_name = "input_#{i}"
      output_name = "output_#{i}"

      output = predict_fn.(params, input)

      [{input_name, input}, {output_name, output}]
    end
    |> List.flatten()
    |> Map.new()

  Safetensors.write!(path, test_data)
end
```

Then in Flax.

```elixir
# need path for import, e.g. transformers.models.resnet.modeling_flax_resnet
run_flax_and_save = fn module, module_args, input_path, output_path, params_path ->
  test_flax =
    """
    import jax
    from typing import Any, Callable, Sequence
    from jax import random, numpy as jnp
    import flax
    from flax import linen as nn

    from functools import partial
    from typing import Optional, Tuple

    from safetensors import safe_open
    from safetensors.flax import save_file

    from transformers.models.resnet.modeling_flax_resnet import #{module}

    def unflatten_dict(d, sep='.'):
      result = {}
      for key, value in d.items():
          parts = key.split(sep)
          node = result
          for part in parts[:-1]:
              node = node.setdefault(part, {})
          node[parts[-1]] = value
      return result

    tensors = {}
    with safe_open("#{input_path}", framework="flax") as f:
        for k in f.keys():
            tensors[k] = f.get_tensor(k)

    model = #{module}(#{module_args})
    key = random.key(0)


    params = {}
    with safe_open("#{params_path}", framework="flax") as f:
        for k in f.keys():
            params[k] = f.get_tensor(k)

    params = unflatten_dict(params)

    out_tensors = tensors.copy()
    input_keys = [key for key in tensors.keys() if key.startswith("input")]
    for input_key in input_keys:  
      input = tensors[input_key]

      output = model.apply(params, input)
      output_key = input_key.replace("input", "output")

      out_tensors[output_key] = output

    save_file(out_tensors, "#{output_path}")
    """

  run_python.(test_flax, [])
end
```

Verify that we get same results.

```elixir
assert_all_close = fn left, right ->
  atol = 1.0e-4
  rtol = 1.0e-4

  equals =
    left
    |> Nx.all_close(right, atol: atol, rtol: rtol)
    |> Nx.backend_transfer(Nx.BinaryBackend)

  equals == Nx.tensor(1, type: :u8, backend: Nx.BinaryBackend)
end

same_result? = fn axon_result, flax_result ->
  assert_all_close.(axon_result, flax_result)
end

verification_results = fn axon_path, flax_path ->
  axon_data = Safetensors.read!(axon_path)
  flax_data = Safetensors.read!(flax_path)

  for output_key <- Map.keys(axon_data), String.starts_with?(output_key, "output"), into: %{} do
    input_key = String.replace(output_key, "output", "input")

    got_same? = same_result?.(axon_data[output_key], flax_data[output_key])

    {output_key,
     %{
       same_result?: got_same?,
       input: axon_data[input_key],
       axon_output: axon_data[output_key],
       flax_output: flax_data[output_key]
     }}
  end
end
```

```elixir
build_model_fn = fn model_code, axon_function_name ->
  ## find set of arguments we need
  out_channels = 2
  kernel_size = 3
  stride = 1
  activation = :relu
  args = [Axon.input(""), out_channels, kernel_size, stride, activation]
  
  model = model_string_fn.(model_code)
  Code.eval_string(model, [], __ENV__)
  
  model = apply_model_fn.(ModelTest, axon_function_name, args)

  Axon.build(model)
end
```

```elixir
get_params = fn init_fn, input_shape, input_type ->
  # get input_shape and input_type
  init_fn.(Nx.template(input_shape, input_type), %{})
end
```

```elixir
verify_model = fn model_code, data_dir, flax_model_name, axon_function_name ->
  # build model
  {init_fn, predict_fn} = build_model_fn.(model_code, axon_function_name)

  # get params and param mapping
  input_shape = {1, 3, 3, 3}
  input_type = :f32

  {:ok, params} = get_params.(init_fn, input_shape, input_type)
  param_mapping = get_param_mapping.()

  # paths for safetensors files
  {axon_params_path, flax_params_path, axon_path, flax_path} =
    safetensor_files.(data_dir, axon_function_name)

  # save params
  save_params.(params, param_mapping, axon_params_path, flax_params_path)

  # run axon, save data
  run_axon_and_save.(predict_fn, params, input_shape, axon_path)

  # run flax, save data
  flax_model_args = 3
  run_flax_and_save.(flax_model_name, flax_model_args, axon_path, flax_path, flax_params_path)

  # check if results are the same 
  results = verification_results.(axon_path, flax_path)

  if Enum.all?(results) do
    {:ok, model_code, results}
  else
    {:verification_failed, model_code, results}
  end
end
```

## Run it all

```elixir
# Get inputs
classes = input_classes.(input) |> Enum.at(1) |> List.wrap() |> dbg()

# Get chains
conversion_chain = ConversionChain.conversion_chain(api_key, system_messages, conversion_messages)
fix_chain = FixChain.fix_chain(api_key)

# for each class
for class <- classes, reduce: %{} do
  converted_classes ->
    {_flax_name, axon_name} = extract_names.(class)

    ## convert the model and verify it gets the same results    
    with {:ok, model} <- convert_model.(conversion_chain, fix_chain, class),
         {:ok, model, _verification_results} <- verify_model.(model) do
      Map.put(converted_classes, axon_name, {:ok, model})
    else
      {:conversion_failed, model} ->
        Map.put(converted_classes, axon_name, {:error, model})

      {:verification_failed, model, verification_results} ->
        Map.put(converted_classes, axon_name, {:error, model, verification_results})
    end
end
```

<!-- livebook:{"offset":19217,"stamp":{"token":"XCP.aIaRrxz3h3MNdEAi_t0VJGrAdkg_94axPPWHrGKgZOvZUrMhr15w8tKk3NLSxmABuMWN9NtPQd4b3Y8IbXG_YAcUxVyJmdaP0qncAYUPhI0g-jRpRQ","version":2}} -->
